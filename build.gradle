
buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:2.2.8"
    }
}


apply plugin: 'war'
apply plugin: 'checkstyle'
apply plugin: 'net.saliman.cobertura'
apply plugin: 'findbugs'
apply plugin: 'jdepend'
apply plugin: 'pmd'

repositories {
    mavenCentral()
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

checkstyle {
    toolVersion = '6.11.2'
}

cobertura {
    coverageCheckLineRate = 100
    coverageCheckHaltOnFailure = false
}

findbugs {
    ignoreFailures = false
}

jdepend {
    ignoreFailures = false
}

pmd {
    ignoreFailures = false
}

configurations {
    // Exclude commons-logging that is a transitive dependency of Spring - we're using logback instead
    all*.exclude group: "commons-logging", module: "commons-logging"
}

dependencies {

    // Spring
    compile 'org.springframework:spring-context:4.2.1.RELEASE'
    compile 'org.springframework:spring-webmvc:4.2.1.RELEASE'
    compile 'org.springframework.data:spring-data-mongodb:1.0.0.RELEASE'
    testCompile 'org.springframework:spring-test:4.2.1.RELEASE'

    // Thymeleaf
    compile 'org.thymeleaf:thymeleaf:2.1.4.RELEASE'
    compile 'org.thymeleaf:thymeleaf-spring4:2.1.4.RELEASE'

    // Jackson JSON Processor
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.2'

    // Servlet API
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'

    // Logging
    compile 'org.slf4j:jcl-over-slf4j:1.7.12'
    compile 'ch.qos.logback:logback-classic:1.1.3'

    // Junit
    testCompile(group: 'junit', name: 'junit', version: '4.12') {
        exclude group: 'org.hamcrest'
    }

    // Hamcrest
    testCompile 'org.hamcrest:hamcrest-all:1.3'

    // SMOG
    testCompile('com.mistraltech.smog:smog-core:1.1.0') {
        exclude group: 'org.hamcrest'
    }
    testCompile('com.mistraltech.smog:smog-javassist:1.0.0') {
        exclude group: 'org.hamcrest'
    }
}

